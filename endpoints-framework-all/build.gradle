plugins {
  id 'com.github.johnrengelman.shadow' version '7.1.2'
  id 'java-library'
}

configurations {
  include
  compile.extendsFrom include
}

jar {
  from {
    configurations.include.collect { it.isDirectory() ? it : zipTree(it) }
  }
}

def repackagedDir = 'endpoints.repackaged'

shadowJar {
  archiveClassifier = null
  relocate 'org.apache', "${repackagedDir}.org.apache"
  relocate 'org.yaml', "${repackagedDir}.org.yaml"
  relocate 'org.joda', "${repackagedDir}.org.joda"
  relocate 'com.fasterxml', "${repackagedDir}.com.fasterxml"
  relocate 'io.swagger', "${repackagedDir}.io.swagger"
  relocate 'com.google.common', "${repackagedDir}.com.google.common"
  relocate 'com.google.api.client', "${repackagedDir}.com.google.api.client"
  relocate 'org.slf4j', "${repackagedDir}.org.slf4j"
  relocate 'com.google.thirdparty', "${repackagedDir}.com.google.thirdparty"

  dependencies {
    exclude(dependency('com.google.appengine:appengine-api-1.0-sdk:.*'))
    exclude(dependency('javax.servlet:javax.servlet-api:.*'))
  }
}

artifacts {
  archives shadowJar
}

dependencies {
  compileOnly project(':endpoints-framework')
  api group: 'com.google.appengine', name: 'appengine-api-1.0-sdk', version: appengineVersion
  api group: 'javax.servlet', name: 'javax.servlet-api', version: servletVersion
}

configureMaven(project, 'Endpoints Framework', 'A framework for building RESTful web APIs.')
